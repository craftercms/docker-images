/*
 * Copyright (C) 2007-2021 Crafter Software Corporation. All Rights Reserved.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License version 3 as published by
 * the Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import org.apache.commons.io.FilenameUtils

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'commons-io', name: 'commons-io', version: '2.6'
    }
}

plugins {
    id "de.undercouch.download" version "4.0.4"
}

if (project.hasProperty("tag")) {
    project.version = project.property("tag")
} else if (project.hasProperty("enterprise")) {
    project.version = "4.1.5E-BETA" // craftercms enterprise version flag
} else {
    project.version = "4.1.5-SNAPSHOT" // craftercms version flag
}

ext {
    imagesDir = "${projectDir}/images"
    imagesBuildDir = "${buildDir}/images"
    tempDir = "${buildDir}/temp"
    libDir = "${projectDir}/lib"

    downloadsRootFolder = "${project.version}"
    authoringBuildTarget = 'main'
    if (project.hasProperty("enterprise")) {
        downloadsRootFolder = "ent/${project.version}"
        authoringBuildTarget = 'enterprise'
    }

    if (project.hasProperty("authoringBundle")) {
        if (project.property("authoringBundle") ==~ /^https?:\/\/.+$/) {
            authoringBundleUrl = project.property("authoringBundle")
            authoringBundleFile = "${tempDir}/" + FilenameUtils.getName(authoringBundleUrl)
            authoringBundleRoot = "${tempDir}/" + FilenameUtils.getBaseName(authoringBundleUrl) + "/crafter"
        } else {
            authoringBundleUrl = ""
            authoringBundleFile = project.property("authoringBundle")
            authoringBundleRoot = "${tempDir}/" + FilenameUtils.getBaseName(authoringBundleFile) + "/crafter"
        }
    } else {
        authoringBundleUrl = "https://downloads.craftercms.org/${downloadsRootFolder}/social/crafter-cms-authoring-${project.version}-linux-x86_64.tar.gz"
        authoringBundleFile = "${tempDir}/" + FilenameUtils.getName(authoringBundleUrl)
        authoringBundleRoot = "${tempDir}/" + FilenameUtils.getBaseName(authoringBundleUrl) + "/crafter"
    }

    if (project.hasProperty("deliveryBundle")) {
        if (project.property("deliveryBundle") ==~ /^https?:\/\/.+$/) {
            deliveryBundleUrl = project.property("deliveryBundle")
            deliveryBundleFile = "${tempDir}/" + FilenameUtils.getName(deliveryBundleUrl)
            deliveryBundleRoot = "${tempDir}/" + FilenameUtils.getBaseName(deliveryBundleUrl) + "/crafter"
        } else {
            deliveryBundleUrl = ""
            deliveryBundleFile = project.property("deliveryBundle")
            deliveryBundleRoot = "${tempDir}/" + FilenameUtils.getBaseName(deliveryBundleFile) + "/crafter"
        }
    } else {
        deliveryBundleUrl = "https://downloads.craftercms.org/${downloadsRootFolder}/social/crafter-cms-delivery-${project.version}-linux-x86_64.tar.gz"
        deliveryBundleFile = "${tempDir}/" + FilenameUtils.getName(deliveryBundleUrl)
        deliveryBundleRoot = "${tempDir}/" + FilenameUtils.getBaseName(deliveryBundleUrl) + "/crafter"
    }

    defaultTomcatBinExcludes = [
        "migration",
        "upgrade",
        "crafter-deployer",
        "opensearch",
        "dbms",
        "cli",
        "mongodb",
        "crafter-cli",
        "apache-tomcat/work/**",
        "apache-tomcat/temp/**",
        "apache-tomcat/webapps/crafter-search*",
        "apache-tomcat/webapps/crafter-social*",
        "apache-tomcat/webapps/crafter-profile*",
        "apache-tomcat/webapps/ROOT",
        "apache-tomcat/webapps/studio",
        "**/*.pid"
    ]

    defaultTomcatLocalBinExcludes = [
        "migration",
        "upgrade",
        "mongodb",
        "apache-tomcat/work/**",
        "apache-tomcat/temp/**",
        "apache-tomcat/webapps/crafter-search*",
        "apache-tomcat/webapps/crafter-social*",
        "apache-tomcat/webapps/crafter-profile*",
        "apache-tomcat/webapps/ROOT",
        "apache-tomcat/webapps/studio",
        "**/*.pid"
    ]
}

task downloadAuthoringBundle(type: Download) {
    src authoringBundleUrl
    overwrite false
    dest authoringBundleFile
}

task downloadDeliveryBundle(type: Download) {
    src deliveryBundleUrl
    overwrite false
    dest deliveryBundleFile
}

task downloadLibSsl(type: Download) {
    src 'https://downloads.craftercms.org/misc/libssl.so.1.1'
    overwrite false
    dest "${libDir}/libssl.so.1.1"
}

task downloadLibCrypto(type: Download) {
    src 'https://downloads.craftercms.org/misc/libcrypto.so.1.1'
    overwrite false
    dest "${libDir}/libcrypto.so.1.1"
}

task expandAuthoringBundle(type: Copy) {
    mustRunAfter downloadAuthoringBundle
    from tarTree(authoringBundleFile)
    into "${tempDir}/" + FilenameUtils.getBaseName(authoringBundleFile)
}

task expandDeliveryBundle(type: Copy) {
    mustRunAfter downloadDeliveryBundle
    from tarTree(deliveryBundleFile)
    into "${tempDir}/" + FilenameUtils.getBaseName(deliveryBundleFile)
}

task prepareAuthoringBundle() {
    dependsOn downloadLibSsl
    dependsOn downloadLibCrypto
    if (!file(authoringBundleRoot).exists()) {
        if (authoringBundleUrl) {
            dependsOn downloadAuthoringBundle
            dependsOn expandAuthoringBundle
        } else if (file(authoringBundleFile).file) {
            dependsOn expandAuthoringBundle
        } else {
            doLast {
                copy {
                    from "${authoringBundleFile}/"
                    into authoringBundleRoot
                }
            }
        }
    }
}

task prepareDeliveryBundle() {
    if (!file(deliveryBundleRoot).exists()) {
        if (deliveryBundleUrl) {
            dependsOn downloadDeliveryBundle
            dependsOn expandDeliveryBundle
        } else if (file(deliveryBundleFile).file) {
            dependsOn expandDeliveryBundle
        }  else {
            doLast {
                copy {
                    from "${deliveryBundleFile}/"
                    into deliveryBundleRoot
                }
            }
        }
    }
}

task copyAuthoringCustomWebapps() {
    dependsOn prepareAuthoringBundle

    if (project.hasProperty("authoringCustomWebapps")) {
        doLast {
            project.property('authoringCustomWebapps').split(',').each { war ->
                copy {
                    from war
                    into "${authoringBundleRoot}/bin/apache-tomcat/webapps"
                }
            }
        }
    }
}

task copyDeliveryCustomWebapps() {
    dependsOn prepareDeliveryBundle

    if (project.hasProperty("deliveryCustomWebapps")) {
        doLast {
            project.property('deliveryCustomWebapps').split(',').each { war ->
                copy {
                    from war
                    into "${deliveryBundleRoot}/bin/apache-tomcat/webapps"
                }
            }
        }
    }
}

task buildAuthoringTomcat(group: "Build", description: "Builds the Authoring Tomcat Docker image") {
    dependsOn prepareAuthoringBundle
    dependsOn copyAuthoringCustomWebapps

    def buildDir = "${imagesBuildDir}/authoring/tomcat"

    doFirst {
        delete buildDir
    }
    doLast {
        copy {
            from "${imagesDir}/authoring/tomcat/"
            into buildDir
        }

        copy {
            from "${authoringBundleRoot}/bin/"
            into "${buildDir}/bin"
            exclude defaultTomcatBinExcludes
        }

        copy {
            from "${libDir}/"
            into "${buildDir}/mariadb-lib-deps"
        }

        exec {
            commandLine 'docker', 'build', '--target', authoringBuildTarget, '-t', "${project.authoringTomcatImageName}:${project.version}", buildDir
        }
    }
}

task buildAuthoringLocal(group: "Build", description: "Builds the Authoring Tomcat Docker Local image") {
    dependsOn prepareAuthoringBundle
    dependsOn copyAuthoringCustomWebapps

    def buildDir = "${imagesBuildDir}/authoring_local/tomcat"

    doFirst {
        delete buildDir
    }
    doLast {
        copy {
            from "${imagesDir}/authoring_local/tomcat/"
            into buildDir
        }

        copy {
            from "${authoringBundleRoot}/bin/"
            into "${buildDir}/bin"
            exclude defaultTomcatLocalBinExcludes
        }

        copy {
            from "${libDir}/"
            into "${buildDir}/mariadb-lib-deps"
        }

        exec {
            commandLine 'docker', 'build', '--target', authoringBuildTarget, '-t', "${project.authoringLocalImageName}:${project.version}", buildDir
        }
    }
}

task buildAuthoringTomcatWithImageMagick(group: "Build", description: "Builds the Authoring Tomcat Docker image, including the ImageMagick binary") {
    dependsOn buildAuthoringTomcat

    def buildDir = "${imagesBuildDir}/authoring/tomcat"

    doLast {
        exec {
            commandLine 'docker', 'build', '--target', 'imagemagick', '-t', "${project.authoringTomcatWithImageMagickImageName}:${project.version}", buildDir
        }
    }
}

task buildDeliveryTomcat(group: "Build", description: "Builds the Delivery Tomcat Docker image") {
    dependsOn prepareDeliveryBundle
    dependsOn copyDeliveryCustomWebapps

    def buildDir = "${imagesBuildDir}/delivery/tomcat"

    doFirst {
        delete buildDir
    }
    doLast {
        copy {
            from "${imagesDir}/delivery/tomcat/"
            into buildDir
        }

        copy {
            from "${deliveryBundleRoot}/bin/"
            into "${buildDir}/bin"
            exclude defaultTomcatBinExcludes
        }

        exec {
            commandLine 'docker', 'build', '-t', "${project.deliveryTomcatImageName}:${project.version}", buildDir
        }
    }
}

task buildDeployer(group: "Build", description: "Builds the Deployer Docker image") {
    dependsOn prepareDeliveryBundle

    def buildDir = "${imagesBuildDir}/deployer"

    doFirst {
        delete buildDir
    }
    doLast {
        copy {
            from "${imagesDir}/deployer/"
            into buildDir
        }
        copy {
            from "${deliveryBundleRoot}/bin/"
            into "${buildDir}/bin"
            exclude "migration"
            exclude "upgrade"
            exclude "cli"
            exclude "mongodb"
            exclude "crafter-cli"
            exclude "apache-tomcat"
            exclude "opensearch"
            exclude "**/*.pid"
        }

        exec {
            commandLine 'docker', 'build', '-t', "${project.deployerImageName}:${project.version}", buildDir
        }
    }
}

task buildProfile(group: "Build", description: "Builds the Profile Docker image")  {
    dependsOn buildDeliveryTomcat

    def buildDir = "${imagesBuildDir}/profile"

    doFirst {
        delete buildDir
    }
    doLast {
        copy {
            duplicatesStrategy = 'include'

            from "${imagesBuildDir}/delivery/tomcat/"
            from "${imagesDir}/profile/"
            into buildDir
        }

        copy {
            from "${deliveryBundleRoot}/bin/apache-tomcat/webapps/crafter-profile.war"
            from "${deliveryBundleRoot}/bin/apache-tomcat/webapps/crafter-profile-admin.war"
            into "${buildDir}/bin/apache-tomcat/webapps"
        }

        exec {
            commandLine 'docker', 'build', '-t', "${project.profileImageName}:${project.version}", buildDir
        }
    }
}

task buildGitSshServer(group: "Build", description: "Builds the Git SSH Server Docker image")  {
    def buildDir = "${imagesBuildDir}/git_ssh_server"

    doFirst {
        delete buildDir
    }
    doLast {
        copy {
            from "${imagesDir}/git_ssh_server/"
            into buildDir
        }

        exec {
            commandLine 'docker', 'build', '-t', "${project.gitSshServerImageName }:${project.version}", buildDir
        }
    }
}

task buildGitHttpsServer(group: "Build", description: "Builds the Git HTTPS Server Docker image")  {
    def buildDir = "${imagesBuildDir}/git_https_server"

    doFirst {
        delete buildDir
    }
    doLast {
        copy {
            from "${imagesDir}/git_https_server/"
            into buildDir
        }

        exec {
            commandLine 'docker', 'build', '-t', "${project.gitHttpsServerImageName}:${project.version}", buildDir
        }
    }
}

task buildLogrotate(group: "Build", description: "Builds the Logrotate Docker image")  {
    def buildDir = "${imagesBuildDir}/logrotate"

    doFirst {
        delete buildDir
    }
    doLast {
        copy {
            from "${imagesDir}/logrotate/"
            into buildDir
        }

        exec {
            commandLine 'docker', 'build', '-t', "${project.logrotateImageName}:${project.version}", buildDir
        }
    }
}

task buildMainImages(group: "Build", description: "Builds main Docker images") {
    dependsOn buildLogrotate
    dependsOn buildAuthoringTomcat
    dependsOn buildAuthoringLocal
    dependsOn buildDeliveryTomcat
    dependsOn buildDeployer
    dependsOn buildGitSshServer
    dependsOn buildGitHttpsServer
    if (project.hasProperty("enterprise")) {
        if (project.hasProperty("imagemagick")) {
            dependsOn buildAuthoringTomcatWithImageMagick
        }
    }
}

task build(group: "Build", description: "Builds all Docker images") {
    dependsOn buildMainImages
    dependsOn buildProfile
}

task clean(group: "Build", description: "Deletes build folder") {
    doLast {
        delete buildDir
    }
}